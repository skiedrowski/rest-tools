buildscript {
	ext.kotlin_version = '1.4.21'

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}
plugins {
	id("maven-publish")
}

description = """REST tools"""

def buildscriptDir = "$rootProject.projectDir/buildscripts"

apply from: "$buildscriptDir/loadEnvironment.gradle"
apply from: "$buildscriptDir/printGradleInfo.gradle"
apply from: "$buildscriptDir/loadVersions.gradle"
apply from: "$buildscriptDir/jacocoMultimodule.gradle"

subprojects {
	apply plugin: 'kotlin'

	tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
		kotlinOptions {
			apiVersion = "1.4"
			languageVersion = "1.4"
			//default suppressWarnings = false
			//default verbose = false
			jvmTarget = "1.8"
			noReflect = true
			noStdlib = true
		}
	}

//	version = '20210222'
	version = 'NEXT-SNAPSHOT'
	group = "com.github.skiedrowski.tools.rest"

	repositories {
		mavenCentral()
		maven {
			url "https://maven.pkg.github.com/${System.getProperty('GH_USERNAME')}/${project.rootProject.name}"
			credentials {
				username System.getProperty('GH_USERNAME')
				password System.getProperty('GH_TOKEN')
			}
		}
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
		compile "com.github.skiedrowski.tools:kotlin-tools:$ver.kt_tools"
		compile "com.github.skiedrowski.tools.cdi:cdi-tools:$ver.cdi_tools"
		compileOnly "javax.enterprise:cdi-api:$ver.cdi_api"
		compileOnly "javax.ws.rs:javax.ws.rs-api:$ver.jaxrs_api"
		compileOnly "javax.annotation:javax.annotation-api:$ver.javax_annotation"
		compileOnly "javax.xml.bind:jaxb-api:$ver.javax_xml_bind"

		testCompile "junit:junit:$ver.junit"
		testCompile "com.nhaarman:mockito-kotlin:$ver.mockito_kotlin"
		// kotlin-reflect is (just) used by mockito-kotlin
		testCompile "org.jetbrains.kotlin:kotlin-reflect"
		testCompile "com.natpryce:hamkrest:$ver.hamkrest"
		testCompile("org.mockito:mockito-core:$ver.mockito") {
			exclude group: "org.hamcrest"
		}
		//provides several implementations of apis with compileOnly dependencies
		testCompile "fish.payara.extras:payara-embedded-all:$ver.payara"
	}

	apply from: "$buildscriptDir/sourcesJar.gradle"
	apply from: "$buildscriptDir/testJar.gradle"
	apply from: "$buildscriptDir/javadocJar.gradle"

	test.maxParallelForks = Runtime.runtime.availableProcessors()

	apply from: "$buildscriptDir/ignoreTestFailuresIfConfiguredInEnv.gradle"
	apply from: "$buildscriptDir/mavenPublishGithubPackages.gradle"
}
apply from: "$buildscriptDir/fixJenkins.gradle"
apply from: "$buildscriptDir/wrapper.gradle"
